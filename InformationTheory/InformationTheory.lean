/-
Information theory basics from Chapter 2 of
   "Elements of Information Theory" by Thomas M. Cover & Joy A. Thomas,
with discrete random variables.

Current main results:
- Information inequality (`information_inequality`)

Current main definitions:
- Probability mass function (`pmf`)
- Entropy (`entropy`), KL-divergence (`kld`)

Author: Sean Welleck
-/
import LLMlean

import Mathlib.Analysis.SpecialFunctions.Log.Basic
import Mathlib.Analysis.Convex.SpecificFunctions.Basic
import Mathlib.Analysis.Convex.Jensen
import Mathlib.Algebra.Order.Field.Basic
import Mathlib.Probability.ProbabilityMassFunction.Basic
import Mathlib.Probability.ProbabilityMassFunction.Constructions

namespace InformationTheory

noncomputable section

open Classical BigOperators Real

variable {Î© : Type*}[Fintype Î©]

section pmf

/- **`pmf Î©`** is a finite-alphabet real-valued probability
mass function. This had advantages over `PMF Î©` for this formalization:
   1. Working in â„ led to fewer conversions to and from â„â‰¥0âˆ,
      e.g., in proofs involving logarithms.
   2. We can use Mathlib's Jensen inequality for `Finset` sums
      (`ConvexOn.le_map_sum` / `ConcaveOn.le_map_sum`).

Also note that `pmf` has an explicit non-negativity condition due to (1).
-/
structure pmf (Î© : Type*)[Fintype Î©] where
   (f : Î© â†’ â„)
   (non_neg : âˆ€ x : Î©, 0 â‰¤ f x)
   (sum_one : HasSum f 1)

-- Allows for `p x` notation.
instance pmfCoe : CoeFun (pmf Î©) (fun _ => Î© â†’ â„) where
  coe p := p.f

theorem hasSum_coe_one (p : pmf Î©) : HasSum p 1 := p.sum_one

-- Probability of any outcome is at most one.
theorem coe_le_one (p : pmf Î©) (x : Î©) : p x â‰¤ 1 := by
  refine' hasSum_le _ (hasSum_ite_eq x (p x)) (hasSum_coe_one p)
  intro x'
  by_cases hx : x' = x
  Â· simp [hx]
  Â· simp [hx, p.non_neg x']

-- Writes has_sum in terms of Finset.sum
theorem pmf.sum_one' (p : pmf Î©) : âˆ‘ x, p x = 1 := by
   rw [â† tsum_fintype]
   exact p.sum_one.tsum_eq

/-- The support of a `pmf` is the set where it is nonzero. -/
def pmf.support (p : pmf Î©) : Finset Î© := {x | p x â‰  0}.toFinset

theorem mem_support_pos (p : pmf Î©) (x : Î©) : x âˆˆ p.support â†” p x > 0 := by
   constructor
   Â·  intro hx
      unfold pmf.support at hx
      simp_all [Set.mem_toFinset]
      rw [lt_iff_le_and_ne]
      constructor
      Â·  exact p.non_neg x
      Â·  simp_all [Ne.symm]
   Â·  intro h
      unfold pmf.support
      simp_all [Set.mem_toFinset]
      linarith

@[simp]
theorem mem_support_iff (p : pmf Î©) (x : Î©) : x âˆˆ p.support â†” p x â‰  0 := by
   unfold pmf.support
   simp [Set.mem_toFinset]

@[simp]
theorem pmf.support_sum (p: pmf Î©) : âˆ‘ x âˆˆ p.support, p x = 1 := by
   rw [â† p.sum_one']
   apply Finset.sum_subset
   Â·  exact Finset.subset_univ p.support
   Â·  simp_all

theorem support_pos (p: pmf Î©) : 0 < âˆ‘ x âˆˆ p.support, p x := by simp

theorem support_eq_inner (p: pmf Î©) (f : Î© â†’ â„) (g : Î© â†’ â„)
  (hsupp: âˆ€ x âˆˆ p.support, f x = g x) :
     âˆ‘ x âˆˆ p.support, f x = âˆ‘ x âˆˆ p.support, g x := by
   simp only [Finset.sum_congr rfl hsupp]

@[simp]
theorem support_nonempty (p : pmf Î©) : p.support.Nonempty := by
   by_contra h
   simp at h
   have h' : âˆ‘ x âˆˆ p.support, p x = âˆ‘ x âˆˆ (âˆ…: Finset Î©), p x := by rw [h]
   simp [Finset.sum_empty] at h'

/--Turn a mathlib `PMF` into our real-valued `pmf` (finite alphabet). -/
def PMF.to_pmf {Î© : Type*} [Fintype Î©] (p : PMF Î©) : pmf Î© where
  f        := fun x â†¦ (p x).toReal
  non_neg  := fun x â†¦ (p x).toReal_nonneg
  sum_one  := by
   have h_has := hasSum_fintype (fun x : Î© â†¦ (p x).toReal)
   have h_sum : (âˆ‘ x : Î©, (p x).toReal) = 1 := by
      rw [â† ENNReal.toReal_sum]
      Â·  have : âˆ‘ a, p a = 1 := by simp [â† tsum_fintype, p.tsum_coe]
         rw [this]
         exact ENNReal.toReal_one
      Â·  intro x hx
         exact p.apply_ne_top x
   simp_all

/--Turn our finite, real-valued `pmf` into mathlibâ€™s `PMF`. -/
def pmf.toPMF (p : pmf Î©) : PMF Î© := by
   let f := fun x â†¦ ENNReal.ofReal (p x)
   refine PMF.ofFinset f p.support ?sum_one ?outside_zero
   Â· simp [pmf.support_sum, f, ENNReal.ofReal_toReal]
     rw [â† ENNReal.ofReal_sum_of_nonneg]
     Â· simp
     Â· intro x hx
       exact p.non_neg x
   Â· intro x hx
     simp_all
     simp [f, hx]

omit [Fintype Î©] in
theorem log_eq_inner {s} (f : Î© â†’ â„) (g : Î© â†’ â„)
   (hsupp: âˆ‘ x âˆˆ s, f x = âˆ‘ x âˆˆ s, g x) :
    log (âˆ‘ x âˆˆ s, f x) = log (âˆ‘ x âˆˆ s, g x) := by simp [hsupp]

end pmf

section entropy
/- 2.1 Entropy -/
-- The entropy $H(X)$ of a discrete random variable $X$
def entropy (p : pmf Î©) : â„ :=
   âˆ‘ x, - ((p x) * (log (p x)))

-- If $Xâˆ¼p(x), the expected value of the random variable $g(X)$
def expected_value (p : pmf Î©) (g : Î© â†’ â„) : â„ :=
   âˆ‘ x, (p x) * (g x)

-- Remark 2.3: The entropy of $X$ is the expected value of the random variable
-- $log (1/p(X))$, where $Xâˆ¼p(x)$.
lemma entropy_as_expectation (p : pmf Î©) : entropy p = expected_value p (Î» x => - log (p x)) := by
   simp [entropy, expected_value]

-- L2.1.1: Entropy is non-negative.
lemma entropy_nonneg (p: pmf Î©) : entropy p â‰¥ 0 := by
   apply Finset.sum_nonneg
   intro x hx
   simp [mul_nonpos_iff]
   constructor
   constructor
   Â· exact p.non_neg x
   Â· rw [log_nonpos_iff] <;> simp [coe_le_one, p.non_neg]

end entropy

section kld

/- We say that `q` dominates `p` if $q(x) = 0$ implies $p(x) = 0$.
   Equivalently, we can say that the measure of `p`
   is absolutely continuous with respect to the measure of `q`.
   We prove the equivalence of this subset form below.
   See: https://en.wikipedia.org/wiki/Absolute_continuity. -/
class Dominates (q: pmf Î©) (p: pmf Î©) where
   abs_cont : p.support âŠ† q.support

theorem dominates_mem_supp (p q: pmf Î©)[Dominates q p]
   (x : Î©) (hsupp: x âˆˆ p.support) : x âˆˆ q.support := Dominates.abs_cont hsupp

lemma dominates_qx0_px0 (p q : pmf Î©)[Dominates q p] (x : Î©) : q x = 0 â†’ p x = 0 := by
   intro hq0
   by_contra h
   have hx_p : x âˆˆ p.support := by
      simpa [mem_support_iff]
   have hx_q : x âˆˆ q.support := dominates_mem_supp p q x hx_p
   simp_all

lemma dominates_pxne0_qxne0 (p q : pmf Î©)[Dominates q p] (x : Î©) : p x â‰  0 â†’ q x â‰  0 := by
   by_contra h
   simp at h
   simp_all [h.2, dominates_qx0_px0 p q x]

lemma qx0_px0_dominates (p q : pmf Î©) (hzero : âˆ€ x, q x = 0 â†’ p x = 0)
   : Dominates q p := by
      refine âŸ¨?hsubsetâŸ©
      intro x hx_p
      have hp_ne : p x â‰  0 := by
         simpa [mem_support_iff] using hx_p
      by_contra hx_not
      have hq0 : q x = 0 := by
         have : x âˆ‰ q.support := by
            simpa [mem_support_iff] using hx_not
         simpa
      have hp0 : p x = 0 := hzero x hq0
      exact hp_ne hp0

/- The subset condition in `Dominates` is equivalent to the condition
   if $q(x) = 0$ implies $p(x) = 0$. -/
theorem dominates_iff_zero_imp_zero (p q : pmf Î©) :
   Dominates q p â†” (âˆ€ x, q x = 0 â†’ p x = 0) := by
   constructor
   Â·  intro hdom
      exact dominates_qx0_px0 p q
   Â·  exact qx0_px0_dominates p q

lemma equal_support (p q : pmf Î©)[Dominates q p] :
  (âˆ‘ x âˆˆ p.support, p x) = (âˆ‘ x âˆˆ q.support, p x) := by
    refine Finset.sum_subset Dominates.abs_cont ?_
    intro x hxq hxp
    simp_all

lemma qsupport_sum (p q : pmf Î©)[Dominates q p] :
  âˆ‘ x âˆˆ q.support, p x = 1 := by rw [â† equal_support, p.support_sum]

theorem dominated_lte (p q: pmf Î©)[Dominates q p] :
   âˆ‘ x âˆˆ p.support, q x â‰¤ âˆ‘ x âˆˆ q.support, q x := by
   refine' Finset.sum_le_sum_of_subset_of_nonneg Dominates.abs_cont _
   intro x _ _
   exact q.non_neg x

theorem dominated_supp_gt_0 (p q: pmf Î©)[Dominates q p] :
   0 < âˆ‘ x âˆˆ pmf.support p, q x := by
   apply Finset.sum_pos
   Â·  intro x hx
      have h := dominates_mem_supp p q x hx
      rw [mem_support_pos] at h
      exact h
   Â·  exact support_nonempty p

-- D2.26 KL divergence
def kld (p q: pmf Î©)[Dominates q p]: â„ :=
   âˆ‘ x, if p x = 0 then 0
        else (p x)*(log (p x / q x))

def kld_supp (p q: pmf Î©)[Dominates q p]: â„ :=
   âˆ‘ x âˆˆ p.support, (p x)*(log (p x / q x))

lemma kld_eq_kld_supp (p q : pmf Î©)[Dominates q p] :
   kld p q = kld_supp p q := by
   rw [kld, kld_supp]
   have h_filter :
      (âˆ‘ x : Î©, if hp : p x = 0 then 0 else p x * log (p x / q x)) =
        âˆ‘ x âˆˆ Finset.filter (fun x : Î© â†¦ p x â‰  0) Finset.univ,
            p x * log (p x / q x) := by
      simp [Finset.sum_filter, mem_support_iff]
   simpa [h_filter, pmf.support]

-- 2.6: Jensen's Inequality And Its Consequences
/- Thm. 2.6.2 (Jensen's Inequality.)
   We prove the concave version:
   If f is a concave function and X is a random variable, then
   ğ”¼[f(X)] â‰¤ f(ğ”¼[X]).  -/
theorem jensens_inequality_concave
   {S : Set â„} (f: â„ â†’ â„) (g: Î© â†’ â„)
   (p: pmf Î©) (hf: ConcaveOn â„ S f) (hmem: âˆ€ x âˆˆ p.support, g x âˆˆ S) :
   âˆ‘ x âˆˆ p.support, (p x) * f (g x) â‰¤ f (âˆ‘ x âˆˆ p.support, (p x) * (g x)) := by
   have hnonneg : âˆ€ (x : Î©), x âˆˆ p.support â†’ 0 â‰¤ p x := by
      intro x _
      exact p.non_neg x
   refine' hf.le_map_sum hnonneg p.support_sum hmem

theorem neg_sum {Î±: Type*} (S : Finset Î±) (f : Î± â†’ â„) :
   - âˆ‘ x âˆˆ S, f x = âˆ‘ x âˆˆ S, - (f x) := by simp

theorem neg_logpq_eq_logqp (p q: pmf Î©)[Dominates q p] (x : Î©) (h : x âˆˆ p.support) :
   - log (p x / q x) = log (q x / p x) := by
   calc
      - log (p x / q x) = - ((log (p x)) - (log (q x))) := by
         rw [log_div]
         simpa using h
         simpa using (dominates_mem_supp p q x h)
      _ = (log (q x) - log (p x)) := by ring
      _ = log (q x / p x) := by
         rw [â† log_div]
         simpa using (dominates_mem_supp p q x h)
         simpa using h

theorem neg_plogpq_eq_plogqp (p q: pmf Î©)[Dominates q p] (x : Î©) (h : x âˆˆ p.support) :
   -(p x * log (p x / q x)) = p x * log (q x / p x) := by
   rw [mul_comm, â†neg_mul, (neg_logpq_eq_logqp p q x h), mul_comm]

theorem log_concave : ConcaveOn â„ (Set.Ioi 0) log := by
   refine' StrictConcaveOn.concaveOn _
   exact strictConcaveOn_log_Ioi

@[simp]
theorem inv_supp_cancel (p : pmf Î©) (x : Î©) (hsupp: x âˆˆ p.support) : (p x)â»Â¹ * (p x) = 1 := by
  have hx_pos := (mem_support_pos p x).mp hsupp
  field_simp [hx_pos.ne']

/- Theorem 2.6.3 (Information inequality)
   Let $p(x), q(x), x âˆˆ X$ be two probability mass functions.
   Then $KL(p || q) â‰¥ 0$.

   This version uses the KL defined on the support.
-/
theorem information_inequality_supp (p q: pmf Î©)[Dominates q p]: kld_supp p q â‰¥ 0 := by
   rw [kld_supp]
   suffices hrev : - kld_supp p q â‰¤ 0 by
    apply neg_nonpos.mp
    exact hrev

   unfold kld_supp
   rw [neg_sum]
   calc
      âˆ‘ x âˆˆ p.support, -(p x * log (p x / q x)) = âˆ‘ x âˆˆ p.support, p x * log (q x / p x) := by
         apply Finset.sum_congr rfl
         intro x hx
         exact neg_plogpq_eq_plogqp p q x hx
      _ â‰¤ log (âˆ‘ x âˆˆ p.support, p x * (q x / p x)) := by
         apply jensens_inequality_concave (S:= (Set.Ioi 0)) log (Î» x => (q x / p x)) p log_concave
         intros x hp
         have hq := dominates_mem_supp p q x hp
         rw [mem_support_pos] at hp
         rw [mem_support_pos] at hq
         simp
         apply div_pos
         Â· exact hq
         Â· exact hp
      _ = log (âˆ‘ x âˆˆ p.support, q x) := by
         ring_nf
         apply log_eq_inner
         apply support_eq_inner
         intro x hx
         rwa [mul_assoc, mul_comm, mul_assoc, inv_supp_cancel p x, mul_one]
      _ â‰¤ log (âˆ‘ x âˆˆ q.support, q x) := by
         apply log_le_log
         Â· exact dominated_supp_gt_0 p q
         Â· apply dominated_lte p q
      _ = 0 := by
         simp [q.support_sum]

/- Theorem 2.6.3 (Information inequality)
   Let $p(x), q(x), x âˆˆ X$ be two probability mass functions.
   Then $KL(p || q) â‰¥ 0$.
-/
theorem information_inequality (p q: pmf Î©)[Dominates q p]: kld p q â‰¥ 0 := by
   rw [kld_eq_kld_supp]
   apply information_inequality_supp

end kld

end
